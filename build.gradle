buildscript {
    apply from: 'manifest.gradle'
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0'

        classpath 'dev.rikka.tools.materialthemebuilder:gradle-plugin:1.3.3'
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
        maven { url 'https://jitpack.io' }
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }
}

subprojects {
    afterEvaluate { project ->
        if (!plugins.hasPlugin('maven-publish')) {
            return
        }

        if (project.parent == rootProject) {
            group = "${groupIdBase}"
        } else {
            group = "${groupIdBase}.${project.parent.name}"
        }
        version = android.defaultConfig.versionName
        println("${project.displayName}: ${group}:${project.name}:${version}")

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    group project.group
                    artifactId project.name
                    version android.defaultConfig.versionName
                    afterEvaluate {
                        from components.release
                    }
                    pom {
                        name = POM_NAME
                        description = POM_DESCRIPTION
                        url = POM_URL
                        licenses {
                            license {
                                name = POM_LICENCE_NAME
                                url = POM_LICENCE_URL
                            }
                        }
                        developers {
                            developer {
                                name = POM_DEVELOPER_NAME
                                url = POM_DEVELOPER_URL
                            }
                        }
                        scm {
                            connection = POM_SCM_CONNECTION
                            url = POM_SCM_URL
                        }
                    }
                }
            }
            repositories {
                maven {
                    name 'ossrh'
                    url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials(PasswordCredentials)
                }
            }
        }
    }

    afterEvaluate {
        if (!plugins.hasPlugin('signing')) {
            return
        }

        android {
            publishing {
                singleVariant("release") {
                    withSourcesJar()
                    withJavadocJar()
                }
            }
        }

        signing {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            def secretKeyRingFile = findProperty("signing.secretKeyRingFile")

            if (secretKeyRingFile != null && file(secretKeyRingFile).exists()) {
                sign publishing.publications
            } else if (signingKey != null) {
                useInMemoryPgpKeys(signingKey, signingPassword)
                sign publishing.publications
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    groupIdBase = "dev.rikka.rikkax"

    POM_INCEPTION_YEAR = "2017"
    POM_PACKAGING = "aar"

    POM_URL = "https://github.com/RikkaApps/RikkaX"
    POM_SCM_URL = "https://github.com/RikkaApps/RikkaX"
    POM_SCM_CONNECTION = "scm:git@github.com:RikkaApps/RikkaX.git"
    POM_SCM_DEV_CONNECTION = "scm:git@github.com:RikkaApps/RikkaX.git"

    POM_LICENCE_NAME = "MIT License"
    POM_LICENCE_URL = "https://github.com/RikkaApps/RikkaX/blob/master/LICENSE"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "rikka"
    POM_DEVELOPER_NAME = "Rikka"
    POM_DEVELOPER_URL = "https://github.com/RikkaW"
}
